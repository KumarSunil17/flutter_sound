static String safePackageName(String prop) {
    prop.replace("-lts", "")
}

static String safePackageVersion(String prop, String version, String ltsVersion) {
    prop.contains("-lts") ? ltsVersion + ".LTS" : version
}

group 'com.dooboolab.fluttersound'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion safeExtGet('flutterFFmpegPackage', 'https').contains("-lts") ? 16 : 24
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


String safeExtGet(String prop, String fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}


dependencies {
    implementation 'com.arthenica:mobile-ffmpeg-' + safePackageName(safeExtGet('flutterFFmpegPackage', 'https')) + ':' + safePackageVersion(safeExtGet('flutterFFmpegPackage', 'https'), '4.3.1', '4.3.1')
    implementation 'androidx.core:core:1.0.0'
    implementation 'androidx.media:media:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'

}

